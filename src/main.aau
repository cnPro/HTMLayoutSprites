
import win.ui;
import win.ui.ctrl.expandeditor;
/*DSG{{*/
winform = ..win.form( acceptfiles=1;bottom=526;parent=...;text="HTMLayout 九宫格助手  作者: lujjjh  编写工具: AAuto";right=775 )
winform.add(  )
/*}}*/

import web.layout;
import web.layout.behavior.splitter;
var wbLayout = web.layout(winform);  

updateImagePath = function(path){
	winform.imagePath = path;
	winform.expandeditor.image = path;
	winform.expandeditor.onchange();
	updateStretch(left = false; top = false; right = false; bottom = false; middle = false);
	
	var htmldir,relativeName;
	while( 
		var dir = path;
		dir =  fsys.getParentDir(dir);
		dir
	) {  
		fsys.enum( dir, { "*.css";"*.htm";"*.html";"*.aau"},
			function(dir,filename,fullpath,findData){ 
				if(filename){ 
					htmldir = fsys.getParentDir(fullpath);
			        return false;
				} 
			} ,false
		);
		
		if( htmldir ){
			relativeName = fsys.path.relative(path,htmldir,false); 
			break;
		}
	}
	if( !htmldir ){
		relativeName = fsys.getFileName(path) 
	}
	
	winform.imageRelativeName = relativeName;
	wbLayout.$1("#background-image").value = relativeName;	
}

openFile = function () {
	import fsys.dlg;
	var filename = fsys.dlg.open("支持的图片格式(*.bmp,*.gif,*.png)|*.bmp;*.gif;*.png|不推荐的图片格式(*.jpg,*.jpeg)|*.jpg;*.jpeg|所有文件|*.*|", , , winform.hwnd);
	if (filename) {
		updateImagePath(filename);
	}
}

import fsys.mime
win.form._defaultProc[0x233/*_WM_DROPFILES*/]  = function(this,hwnd, message, wParam, lParam){
	var filename = win.getDropFile(wParam )[1]; 
	if ( string.startWith(fsys.mime.fromFile(filename),"image/",true) ) { 
		updateImagePath(filename);
	}
}


zoomIn = function () {
	for (i = -5; 4) {
		winform.expandeditor.zoom += 0.1;
		win.delay(..math.abs(i) * 2);
	}
}

zoomOut = function () {
	for (i = -5; 4) {
		winform.expandeditor.zoom -= 0.1;
		win.delay(..math.abs(i) * 2);
	}
}

generateCode = function () {
	if (!winform.imagePath) return; 
	var pos = winform.expandeditor.position;
	var code = 'background-image:url(%s);\r\nbackground-repeat: expand%s;\r\nbackground-position: %dpx %dpx %dpx %dpx;';
	code = string.format(code,winform.imageRelativeName, wbLayout.$1("#expand-stretch").value, pos.top, pos.right, pos.bottom, pos.left);
	return code;
}

preview = function () {
	if (!winform.imagePath) return;
	var imagePath = string.replace(winform.imagePath, ":", function (s) {
		var a, b, c = string.unpack(string.fromto(s, 0, 65001)); 
		return string.format("%%%X%%%X%%%X", a, b, c);
	});
	var pos = winform.expandeditor.position;
	var preview = wbLayout.$1(".sidebar .preview");
	preview.style["background-image"] = "url(" + imagePath + ")";
	preview.style["background-repeat"] = "expand" + wbLayout.$1("#expand-stretch").value;
	preview.style["background-position"] = string.format("%dpx %dpx %dpx %dpx", pos.top, pos.right, pos.bottom, pos.left);
}

updateStretch = function (stretchOpt) {
	if (stretchOpt) {
		for (k, v in stretchOpt)
			wbLayout.$1('.stretch widget[role=' + k + ']').state.checked = v;
	}
	var stretch = wbLayout.$('.stretch widget:checked');
	var str = "";
	for (k, v in stretch)
		str += " stretch-" + v.role;
	wbLayout.$1("#expand-stretch").value = str;
	preview();
}

copyCode = function () {
	import win.clip;
	
	if (!winform.imagePath) {
		winform.msgbox("请先打开图片文件。", "错误：", 0x30/*_MB_ICONEXCLAMATION*/);
		return;
	}
	
	win.clip.write(generateCode());
	winform.msgbox("复制成功！", "提示：");
}

import win.ui.accelerator;

var accel = win.ui.accelerator({
	{ ctrl = true; vkey = 'O'#; oncommand = openFile }
	{ ctrl = true; vkey = 0xBB/*_VK_PLUS*/; oncommand = zoomIn }
	{ ctrl = true; vkey = 0xBD/*_VK_MINU*/; oncommand = zoomOut }
}, winform);

import win.ui.menu;
var menu = win.ui.menu(winform);
var mnFile = win.ui.popmenu(winform);
mnFile.add('打开...\tCtrl+O', openFile);
mnFile.add();
mnFile.add("复制代码(&C)", copyCode);
mnFile.add();
mnFile.add('退出\tAlt+F4', function () {
	winform.close();
});
var mnView = win.ui.popmenu(winform);
mnView.add('放大(&I)\tCtrl+=', zoomIn);
mnView.add('缩小(&O)\tCtrl+-', zoomOut);
menu.add("文件(&F)", mnFile);
menu.add("视图(&V)", mnView);

wbLayout.onControlCreated = {
	expandeditor = function( ltEle,ltCtrl ) {
	 	winform.expandeditor = ltCtrl;
	 	winform.expandeditor.onchange = function () {
			var pos = winform.expandeditor.position;
			wbLayout.$1("#background-position").value = string.format("%dpx %dpx %dpx %dpx", pos.top, pos.right, pos.bottom, pos.left);
			preview();
		}
	}
}

wbLayout.onButtonClick = function (ltTarget,ltEle,reason,behaviorParams) {
	select (ltEle.id) {
		case "toolbar_open" {
			openFile();
		}
		case "toolbar_zoom_in" {
			zoomIn();
		}
		case "toolbar_zoom_out" {
			zoomOut();
		}
		case "toolbar_copy" {
			copyCode();
		}
		case null {
			updateStretch();
		}
	}
}
 
import win.clip;
wbLayout.sinking = {
	onMenuItemClick = {
		["htmlarea-copy"] = function (ltTarget,ltEle,reason,behaviorParams) {
			//得到当前弹出菜单的节点
			var ltOwner = ..web.layout.element( behaviorParams.he )  
			var str = ltOwner.innerText; 
			
			//修正HTMLayout复制后丢失换行符问题
			str  = ..string.replace( str,'\r\n*','\r\n');
			win.clip.write(str)
			return true; //阻止默认行为
		}
	}
}

wbLayout.go("/layout/index.htm");

updateImagePath( "/res/button-hover.png" );
winform.expandeditor.image = string.load(winform.imagePath);
winform.expandeditor[["pos_"]] = ::RECT(4, 4, 4, 20);
winform.expandeditor.onchange();
updateStretch(left = true; top = true; right = true; bottom = true; middle = true);

winform.show();
win.loopMessage();
